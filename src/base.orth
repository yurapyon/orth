; TODO need
;   cleave spread apply
;   fold! is well defined like each!
;   map iterator
;     get keys as a vec and iterate over that
; TODO want
;   add more documentation
;   @alias
;     { ref unwrap } dip @
;   strcollect!
;   return stack accessors

{ over value-type-of :slice eq? def } :@ #t def
{ #f def } :@value @

<map> :doc-table @
{ doc-table mset! } :doc! @
{ doc-table mget "docstring not found" unwrap,default } :doc @

{ over doc! @ } :@doc @
"( value symbol doc-string -- )\ndefine with a doc string" :@doc doc!

{ { symbol>string display } keep
  ": " display
  ; TODO ref display should be ref write
  { ref drop display } keep newline
  doc display newline
} :.doc
"( symbol -- )\nprint documentation for a word"
@doc

; TODO
; { over value-type-of :unmanaged-ptr eq?
  ; { over ffi-type-of :record eq?
    ; { over record-type-of :callable eq?
      ; ; what to do here ?
      ; { { callable-data } { callable-q eval,restore } bi }
      ; { eval,restore' }
      ; if }
    ; { eval,restore' }
    ; if }
  ; { eval,restore' }
  ; if }
; } :eval,restore @

{ ; TODO

{ dup value-type-of :rc-ptr eq?
  { dup rc-type-of :record eq?
    { dup record-type-of :callable eq?
      { { callable.data } { callable.q eval } bi }
      { eval' }
      ? eval' }
    { eval' }
    ? eval' }
  { eval' }
  ? eval'
} :eval @

} drop

{ eval' } :eval @

{ ref unwrap eval
} :exec
"( symbol -- )\nevaluate a symbol"
@doc

; =====

{ "\n" display
} :newline
"( -- )\ndisplay a newline"
@doc

{ "TOP| " display 0 stack.index display newline
} :.
"( x -- )\ndisplay the top of the stack"
@doc

{ "STACK| len: " display stack.len display newline
  stack.iterator
  { { "    " display display "| " display } keep
    stack.index display newline }
  each!
} :.stack
"( -- )\ndisplay the stack"
@doc

; =====

{ over over
} :2dup
"( x y -- x y x y )"
@doc

{ pick pick pick
} :3dup
"( x y z -- x y z x y z )"
@doc

"( x -- )\npop a value off the stack" :drop doc!

{ drop drop
} :2drop
"( x y -- )\npop two values off the stack"
@doc

{ drop drop drop
} :3drop
"( x y z -- )\npop three values off the stack"
@doc

{ { drop } dip
} :nip
"( x y z -- x z )\nremove the second from the top value from the stack"
@doc

{ { 2drop } dip
} :2nip
"( x y z -- z )\nremove the second and third from the top values from the stack"
@doc

{ pick pick
} :2over @

{ { swap } dip swap
} :rot< @

{ swap { swap } dip
} :rot> @

{ swap rot<
} :flip
"( x y z -- z y x )"
@doc

; =====

{ 1 eval,restore
} :dip
"( x q -- x )\nremove x from the stack. eval q. then restore x to the stack"
@doc

; TODO these can just use 2 eval,restore 3 eval,restore etc
{ swap { dip } dip
} :2dip
"( x y q -- x y )\nremove x and y from the stack. eval q. then restore x and y to the stack"
@doc

{ swap { 2dip } dip
} :3dip
"( x y z q -- x y z )\nremove x, y, and z from the stack. eval q. then restore x, y, and z to the stack"
@doc

{ swap { 3dip } dip
} :4dip
"( w x y z q -- w x y z )\nremove w, x, y, and z from the stack. eval q. then restore w, x, y, and z to the stack"
@doc

{ over { eval } dip
} :keep
"( x q -- x )\napply q to x. then restore x to the stack"
@doc

{ { 2dup } dip 2dip
} :2keep
"( x y q -- x y )\napply q to x and y. then restore x and y to the stack"
@doc

{ { 3dup } dip 3dip
} :3keep
"( x y z q -- x y z )\napply q to x, y, and z. then restore x, y, and z to the stack"
@doc

{ { keep } dip eval
} :bi
"( x p q -- )\napply p to x. then apply q to x"
@doc

{ { 2keep } dip eval
} :2bi
"( x y p q -- )\napply p to x and y. then apply q to x and y"
@doc

{ { 3keep } dip eval
} :3bi
"( x y z p q -- )\napply p to x, y and z. then apply q to x, y, and z"
@doc

{ { { keep } dip keep } dip eval
} :tri
"( x p q r -- )\napply p to x. then apply q to x. then apply r to x"
@doc

{ { { 2keep } dip 2keep } dip eval
} :2tri
"( x y p q r -- )\napply p to x and y. then apply q to x and y. then apply r to x and y"
@doc

{ { { 3keep } dip 3keep } dip eval
} :3tri
"( x y z p q r -- )\napply p to x, y, and z. then apply q to x, y, and z. then apply r to x, y, and z"
@doc

{ { dip } dip eval
} :bi,s
"( x y p q -- )\napply p to x. then apply q to y"
@doc

{ { 2dip } dip eval
} :2bi,s @

{ { { 2dip } dip dip } dip eval
} :tri,s @

{ { 4dip } 2dip 2bi,s
} :2tri,s @

{ dup bi,s
} :bi,a @

{ dup 2bi,s
} :2bi,a @

{ dup dup tri,s
} :tri,a @

{ dup dup 2tri,s
} :2tri,a @

{ bi,a and
} :both? @

{ bi,a or
} :either? @

; =====

{ ? eval } :if @
{ { } if } :when @
{ { } swap if } :unless @

{ 2dup slen 1 - >=
  { sget eval }
  { 2dup sget
    ; n arr branch
    0 swap sget 2dip
    ; #t/f n arr
    rot<
    { sget 1 swap sget eval }
    { { 1 + } dip cond' }
    if }
  if
} :cond'
; ( n slice -- )
@

{ 0 swap cond'
} :cond @


; TODO
; ?if
; case ?
;   cond is probably fine

; types ==

; TODO
{
{ dup unmanaged-ptr?
  { dup record?
    { record-type-of }
    { ffi-type-of }
    if
    }
  drop
  { ffi-type-of }
  { value-type-of }
  if
} :type-of @
} drop

{ value-type-of :int eq? } :int? @
{ value-type-of :float eq? } :float? @
{ value-type-of :char eq? } :char? @
{ value-type-of :boolean eq? } :boolean? @
{ value-type-of :sentinel eq? } :sentinel? @
{ value-type-of :string-literal eq? } :string-literal? @
{ value-type-of :word eq? } :word? @
{ value-type-of :symbol eq? } :symbol? @
{ value-type-of :slice eq? } :slice? @
{ value-type-of :ffi-fn eq? } :ffi-fn? @
{ value-type-of :rc-ptr eq? } :rc-ptr? @
{ value-type-of :unmanaged-ptr eq? } :unmanaged-ptr? @

{ dup rc-ptr?
  { rc-type-of :string eq? }
  { drop #f }
  if
} :string? @

{ dup rc-ptr?
  { rc-type-of :record eq? }
  { drop #f }
  if
} :record? @

{ dup rc-ptr?
  { rc-type-of :vec eq? }
  { drop #f }
  if
} :vec? @

{ dup rc-ptr?
  { rc-type-of :map eq? }
  { drop #f }
  if
} :map? @

{ dup rc-ptr?
  { rc-type-of :file eq? }
  { drop #f }
  if
} :file? @

; math ==

{ swap < } :> @
{ > not } :<= @
{ < not } :>= @

{ 2 mod 0 = } :even? @
{ even? not } :odd? @

{ { > } 2keep ? } :max @
{ { < } 2keep ? } :min @
{ min max } :clamp
"( min max val -- clamped )"
@doc

{ dup 0 < { neg } when
} :abs @

{ pick swap over { - } 2bi,a
  swap mod +
} :wrap
"( min max val -- wrapped )"
@doc

; =====

; errors and optionals
; error:
;  ok-val  #t
;  err-val #f
; optional:
;  ok-val #t
;  #f     #f

; TODO get rid of this ?
; have unwrap that prints an error message before panic
{ { } { "panic" . panic } if
} :unwrap @

; val t/f def -- val or default
{ { swap } dip ?
} :unwrap,default @

;

; TODO for now this needs to be here because of unwrap
; fix that and move back up by eval
; :eval,restore' ref unwrap :eval,restore @
{ eval,restore' } :eval,restore @

; =====

{ vlen 0 swap
} :vbounds @

; TODO write in zig
{ <string>,clone string-append!
} :string-append @

; =====

#sentinel :[ @

{ over #sentinel eq?
  { nip }
  { { vpush! } keep ]vec' }
  if
} :]vec' @

{ <vec> ]vec' { vreverse! } keep
} :]vec
"( #sentinel ... a b c -- vec )"
@doc

{ over #sentinel eq?
  { nip }
  { { { swap } dip mset! } keep ]map' }
  if
} :]map' @

{ <map> ]map'
} :]map
"( #sentinel ... k1 v1 k2 v2 -- map )"
@doc

; slices ===

{ ; TODO

{ 2dup { slen } dip <=
  { 3drop #f #f }
  { 3dup swap sget eq?
    { 2nip #t }
    { 1 + index-of,eq' }
    if }
  if
} :index-of,eq'
; ( test slice n -- )
@

{ 0 index-of,eq'
} :index-of,eq @

} drop

; records ===

; TODO
;   autogenerate docstrings
;   type check accessor arguments ?
;   eqv?
;   use type-info rather than putting display and write in the records themselves

{ 0 swap rget } :record-type.type @
{ 1 swap rget } :record-type.name @
{ 2 swap rget } :record-type.field-tags @
{ 3 swap rget } :record-type.display-fn @
{ 4 swap rget } :record-type.write-fn @
{ 0 swap rset! } :record-type.type! @
{ 1 swap rset! } :record-type.name! @
{ 2 swap rset! } :record-type.field-tags! @
{ 3 swap rset! } :record-type.display-fn! @
{ 4 swap rset! } :record-type.write-fn! @

5 <record>
{ { dup downgrade unwrap  swap record-type.type! }
  { :record-type          swap record-type.name! }
  { { :name :field-tags :display-fn :write-fn } swap record-type.field-tags! }
  tri } keep
:%record-type @

{ 5 <record>
  { { %record-type swap record-type.type! }
    { record-type.name! }
    { record-type.field-tags! }
    tri } keep
} :<record-type> @

{ 0 swap rget } :record.type @
{ 0 swap rset! } :record.type! @

{ record.type record-type.field-tags slen
} :record.field-ct @

; { record.type record-type.field-tags index-of,eq
; } :record.field-index @

{ 2dup { record.field-ct } dip <=
  { drop }
  { { over record.field-ct swap - swap rset! } 2keep
    1 + construct' }
  if
} :construct'
; ( record n -- )
@

{ dup record-type.field-tags slen 1 + <record>
  { record.type! } keep
  0 construct'
} :construct @

; { record.type eq?
; } :is? @

; def-record macros

{ dup symbol>string "%" string-append string>symbol { <record-type> } dip @
} :def-record'type @

; TODO maybe dont have this
; %type construct is probably fine
{ nip
  :construct symbol>word swap
  symbol>string
  { "%" string-append string>symbol symbol>word }
  { "make-" string-append string>symbol }
  bi
  ; \construct \%cell :make-cell
  { <vec>
    { vpush! } keep
    { vpush! } keep
    vec>slice } dip
  @
} :def-record'constructor @

{ nip
  :eq? symbol>word swap
  symbol>string
  { "%" string-append string>symbol symbol>word }
  { "?" swap string-append string>symbol }
  bi
  :record.type symbol>word swap
  ; \eq? \%cell \record.type :cell?
  { <vec>
    { vpush! } keep
    { vpush! } keep
    { vpush! } keep
    vec>slice } dip
  @
} :def-record'predicate @

{ 2dup swap slen >=
  { 3drop }
  { 3dup
    { swap sget symbol>string swap string-append } keep
    1 +
    ; ... "type-field-name" n
    2dup
    ; getter
    { string>symbol :rget symbol>word :swap symbol>word } dip
    <vec>
    { vpush! } keep
    { vpush! } keep
    { vpush! } keep
    vec>slice
    swap
    @
    ; setter
    { "!" swap string-append string>symbol
      :rset! symbol>word :swap symbol>word } dip
    <vec>
    { vpush! } keep
    { vpush! } keep
    { vpush! } keep
    vec>slice
    swap
    @
    1 +
    def-record'accessors'
  }
  if
} :def-record'accessors'
; ( name-str field-names n -- )
@

{ symbol>string "." swap string-append! swap 0 def-record'accessors'
} :def-record'accessors
; ( field-names symbol -- )
@

{ { def-record'type } 2keep
  { def-record'constructor } 2keep
  { def-record'predicate } 2keep
  def-record'accessors
} :@record
"( field-names symbol -- )"
@doc

; iterators =====

; iterators should always return 2 objects
; value/undefined #t/f

; TODO
;   chunks iterator
;     array>map for 'map literals'

{ :obj :check :next } :iterator @record
; :make-iterator ref unwrap :<iterator> @
{ make-iterator } :<iterator> @
{ dup iterator.check eval } :should-continue? @
{ dup iterator.next eval } :next! @

;

{ :val } :alwayser @record
{ make-alwayser
  { drop #t }
  { iterator.obj alwayser.val #t }
  <iterator>
} :always @

;

{ :iter :fn } :mapper @record
{ make-mapper
  { iterator.obj mapper.iter should-continue? }
  { dup should-continue?
    { iterator.obj
      { mapper.iter next! }
      { swap
        { mapper.fn eval #t }
        { drop #f }
        if }
      bi }
    { drop #f #f }
    if
    }
  <iterator>
} :map @

;

{ :iter :fn } :filterer @record
{ make-filterer
  { iterator.obj filterer.iter should-continue? }
  { dup should-continue?
    { iterator.obj
      { filterer.iter next! }
      { swap
        { { dup } dip filterer.fn eval #t }
        { drop #f #f }
        if }
      bi
      and
      }
    { drop #f #f }
    if
    }
  <iterator>
} :filter @

;

{ over should-continue?
  { { swap next!
      { swap eval }
      { drop }
      if
    } 2keep
    each! }
  { 2drop }
  if
} :each!
"( iter q -- )"
@doc

{ pick should-continue?
  { { over next! } dip
    swap
    { { eval } keep fold! }
    { nip fold! }
    if }
  { drop nip }
  if
} :fold!
"( iter acc fn -- )"
@doc

{ <vec> { over vpush! } fold! } :vcollect! @

;

; TODO fix zip

{ :first :second } :pair @record
; :make-pair ref unwrap :<pair> @
{ make-pair } :<pair> @

{ { pair.first } { pair.second } bi
} :pair>stack @

{ :a :b } :zipper @record

{ { zipper.a } { zipper.b } bi { should-continue? } both?
} :zipper.should-continue? @

{ make-zipper
  { iterator.obj zipper.should-continue? }
  { dup should-continue?
    { iterator.obj { zipper.a } { zipper.b } bi { next! } bi,a
      { swap } dip and
      { <pair> #t }
      { 2drop #f #f }
      if }
    { drop #f #f }
    if
    }
  <iterator>
} :zip @

;

{ :inc :stop :acc } :counter @record

{ { counter.acc } { counter.stop } bi <
} :counter.should-continue? @

{ { { counter.acc } { counter.inc } bi + } keep counter.acc!
} :counter.increment! @

; TODO make sure inc != 0
; if you want to cound with floats, this 0 needs to be a float
{ 0 make-counter
  { iterator.obj counter.should-continue? }
  { dup should-continue?
    { iterator.obj { counter.acc } { counter.increment! } bi #t }
    { drop #f }
    if
    }
  <iterator>
} :count @

;

{ { 1 swap count } { always } bi,s
  zip
} :sequence.iterator
"( len seq -- iter )"
@doc

{ { slen } keep sequence.iterator
  { pair>stack sget } map
} :slice.iterator @

{ { vlen } keep sequence.iterator
  { pair>stack vget } map
} :vec.iterator @

{ { strlen } keep sequence.iterator
  { pair>stack strget } map
} :string.iterator @

{ stack.len { always } { 1 swap count } bi
  zip
  { pair>stack - 1 - } map
} :stack.iterator @

; =====

{ :data :q } :callable @record

{ :saved-val :q } :curried @record

{ make-curried
  { { curried.saved-val } { curried.q eval } bi }
  make-callable
} :curry @

{ :p :q } :composed @record

{ make-composed
  { { composed.p eval } { composed.q eval } bi }
  make-callable
} :compose @

; ports ===

{ :data
  :read-char-fn
  :peek-char-fn
  :read-delimiter-fn
  :read-all-fn
} :input-port @record

{ { input-port.data } { input-port.read-char-fn } bi eval
} :read-char,p @

{ { input-port.data } { input-port.peek-char-fn } bi eval
} :peek-char,p @

{ { input-port.data } { input-port.read-delimiter-fn } bi eval
} :read-delimiter,p @

{ #\newline swap read-delimiter,p
} :read-line,p @

{ { input-port.data } { input-port.read-all-fn } bi eval
} :read-all,p @

{ :data
  :display-fn
  :write-fn
  :write-char
  :write-all-fn
} :output-port @record

{ { output-port.data } { output-port.display-fn } bi eval
} :display,p,unformatted
; ( value port -- )
@

{ "error: type error\nexpected " display
  record-type.name display newline
  type-error
} :type-error' @

{ dup record? { %output-port type-error' } unless
  dup output-port? { %output-port type-error' } unless
  over rc-ptr?
  { over rc-ptr.weak?
    { display,p,unformatted }
    { over record?
      { over record.type record-type.display-fn dup sentinel?
        ; TODO have a better default way to show records
        { drop display,p,unformatted }
        { eval }
        if }
      { over rc-type-of type-info-table
        ; TODO handle not found
        mget unwrap
        type-info.display-fn eval }
      if }
    if }
  { display,p,unformatted }
  if
} :display,p
; ( value port -- )
@

{ { output-port.data } { output-port.write-fn } bi eval
} :write,p,unformatted
; ( value port -- )
@

; TODO raise type error rather than panic
{ dup record? { panic } unless
  dup output-port? { panic } unless
  over rc-ptr?
  { over rc-ptr.weak?
    { write,p,unformatted }
    { over record?
      { over record.type record-type.write-fn dup sentinel?
        ; TODO have a better default way to show records
        { drop write,p,unformatted }
        { eval }
        if }
      { over rc-type-of type-info-table
        ; TODO handle not found
        mget unwrap
        type-info.write-fn eval }
      if }
    if }
  { write,p,unformatted }
  if
} :write,p
; ( value port -- )
@

; type info ===

; wont work for primitive types
; can probably work for slices

<map> :type-info-table @
{ :display-fn :write-fn } :type-info @record

; TODO construct,slice
{ { { 0 swap sget } { 1 swap sget } bi make-type-info } dip
  type-info-table mset!
} :@type-info @

{ { "v[ " over display,p
    swap vec.iterator
    { over display,p ", " over display,p }
    each!
    "]" swap display,p }
  { "[ " over display,p
    swap vec.iterator
    { over write,p " " over display,p }
    each!
    "]vec" swap display,p }
} :vec @type-info

{ { swap string.iterator
    { over display,p }
    each!
    drop }
  { "\"" over display,p
    swap string.iterator
    { over display,p }
    each!
    "\"" swap display,p }
} :string @type-info

{ { "f(" over display,p
    swap file.filepath over display,p
    ")" swap display,p }
  ; TODO
  #f
} :file @type-info

; string ports ===

{ :string :n } :string-input-port-info @record

{ 0 make-string-input-port-info
  { dup { string-input-port-info.string strlen } { string-input-port-info.n } bi <=
    { #f #f }
    { { { string-input-port-info.n } { string-input-port-info.string } bi strget #t } keep
      { string-input-port-info.n 1 + } { string-input-port-info.n! } bi }
    if }
  { { string-input-port-info.n } { string-input-port-info.string } bi strget }
  #f
  ; TODO take substring
  { string-input-port-info.string }
  make-input-port
} :<string-input-port> @

; file ports ===

{
:file.read-char ref unwrap
:file.peek-char ref unwrap
:file.read-delimiter ref unwrap
:file.read-all ref unwrap
make-input-port
} :<file-input-port> @

{ "r" swap <file>,open
  { <file-input-port> #t }
  { #f }
  if
} :open-input-file @

{
:file.display ref unwrap
:file.write ref unwrap
:file.write-char ref unwrap
:file.write-all ref unwrap
make-output-port
} :<file-output-port> @

{ "w" swap <file>,open
  { <file-output-port> #t }
  { #f }
  if
} :open-output-file @

:in <file>,std unwrap :stdin-file @
stdin-file <file-input-port> :stdin @

{ stdin read-line,p } :read-line @

:out <file>,std unwrap :stdout-file @
stdout-file <file-output-port> :stdout @

{ stdout display,p } :display @
{ stdout display,p,unformatted } :display,unformatted @
{ stdout write,p } :write @
{ stdout write,p,unformatted } :write,unformatted @

; TODO
; :err <file>,std unwrap :stderr @
